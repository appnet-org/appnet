start: (statement ";")*

statement: select_statement
       | create_table_statement
       | set_statement
       | create_table_as_statement
       | insert_value_statement
       | insert_select_statement
       | update_statement

select_statement: "SELECT" select_list "FROM" table_name join_clause? where_clause?

select_list: column_name ("," column_name)*

join_clause: "JOIN" table_name "ON" value "=" value

create_table_statement: "CREATE" "TABLE" table_name "(" column_definition ("," column_definition)* ")"

create_table_as_statement: "CREATE" "TABLE" table_name "AS" select_statement

set_statement: "SET" assignment ("," assignment)*

insert_value_statement: "INSERT INTO" table_name column_list? "VALUES" value_list*

insert_select_statement: "INSERT INTO" table_name column_list? select_statement

update_statement: "UPDATE" table_name "SET" update_list [where_clause]

update_list: assignment ("," assignment)*

where_clause: "WHERE" search_condition

column_list: "(" column_name ("," column_name)* ")"

column_definition: column_name data_type [length]

value_list: "(" value ("," value)* ")"

assignment: identifier "=" value

data_type: CNAME

length: "(" NUMBER ")"

value: function -> function
     | column_name
     | table_name "." CNAME -> column_field
     | NUMBER -> number
     | CNAME -> cname
     | quoted_string -> string


column_name: "*" -> all | CNAME -> cname

table_name: CNAME

identifier: CNAME

search_condition: comparison_condition
       | search_condition "AND" search_condition -> search_and_condition
       | search_condition "OR" search_condition -> search_or_condition
       | "(" search_condition ")"

comparison_condition: value comparison_operator value

comparison_operator: "=" -> eq
                   | "<>" -> neq
                   | "<" -> l
                   | ">" -> g
                   | "<=" -> le
                   | ">=" -> ge

function: "random()" -> random_func

quoted_string: /'[^']*'/

%import common.SIGNED_NUMBER  -> NUMBER
%import common.SQL_COMMENT
%import common.CNAME
%import common.WS
%ignore WS
%ignore SQL_COMMENT
